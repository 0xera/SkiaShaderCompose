plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.example.skiashaderonandroid"
        minSdk 24
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        ndk {
            abiFilters "x86_64", "arm64-v8a"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

}


configurations {
    skikoNativeX64
    skikoNativeArm64
}

def jniDir = "${projectDir.absolutePath}/src/main/jniLibs"

def unzipTaskX64 = tasks.register("unzipNativeX64", Copy) {
    def skikoNativeX64 = configurations.skikoNativeX64
    from(skikoNativeX64.getFiles().collect { zipTree(it) })
    into(file("$jniDir/x86_64"))
}

def unzipTaskArm64 = tasks.register("unzipNativeArm64", Copy) {
    def skikoNativeArm64 = configurations.skikoNativeArm64
    from(skikoNativeArm64.getFiles().collect { zipTree(it) })
    into(file("$jniDir/arm64-v8a"))
}

dependencies {
    def skiko_version = "0.7.15"
    implementation "org.jetbrains.skiko:skiko-android:$skiko_version"

    skikoNativeX64("org.jetbrains.skiko:skiko-android-runtime-x64:$skiko_version")
    skikoNativeArm64("org.jetbrains.skiko:skiko-android-runtime-arm64:$skiko_version")
}

tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach {
    dependsOn(unzipTaskX64)
    dependsOn(unzipTaskArm64)
}
